apply plugin: 'maven-publish'
apply plugin: 'signing'

tasks.register("sourceJar", Jar) {
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
    archiveClassifier.set('sources')
}

Properties localProps = new Properties()
File localProperties = new File(rootProject.rootDir, "local.properties")
if (localProperties.exists() && localProperties.isFile()) {
    localProperties.withInputStream { localProps.load(it) }
}

project.ext["signing.keyId"] = localProps.getProperty("signing.keyId")
project.ext["signing.secretKeyRingFile"] = localProps.getProperty("signing.secretKeyRingFile")
project.ext["signing.password"] = localProps.getProperty("signing.password")

//tasks {
//    dokka {
//        outputFormat = "javadoc"
//        outputDirectory = "$buildDir/javadoc"
//        moduleName = rootProject.name
//    }
//}
//
//val dokkaJar by tasks.creating(Jar::class) {
//    group = JavaBasePlugin.DOCUMENTATION_GROUP
//    description = "Assembles Kotlin docs with Dokka"
//    archiveClassifier.set("javadoc")
//    from(tasks.dokka)
//    dependsOn(tasks.dokka)
//}
//
//val sourcesJar by tasks.registering(Jar::class) {
//    archiveClassifier.set("sources")
//    from(android.sourceSets.getByName("main").java.srcDirs)
//}

afterEvaluate {
    publishing {
        publications {
            releaseAar(MavenPublication) {
                from components.release
                artifact sourceJar

                groupId = "io.github.nuclominus"
                artifactId = "easynotificationbuilder"
                version = "0.2.0"

                pom {
                    name = 'Easy Notification Builder'
                    description = 'Simple builder for push notifications'
                    url = 'https://github.com/Nuclominus/easynotificationbuilder'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = 'nuclominus'
                            name = 'Roman Kosko'
                            email = '9DGRoman@gmail.com'
                        }
                    }

                    scm {
                        url = 'https://github.com/Nuclominus/easynotificationbuilder'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "sonatypeStaging"
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials {
                    username = localProps.getProperty("ossrhUsername")
                    password = localProps.getProperty("ossrhPassword")
                }
            }
        }
    }

    signing {
        sign(publishing.publications)
    }
}